# Copyright (c) 2022 Nik Silver
#
# This source code is released for free distribution under the terms of the
# GNU General Public License version 2 or later.
#
# Elmish tags format:
#   Kinds
#   - m module
#   - n namespace (ie a module that's renamed)
#   - t type
#   - c constructor (within a type)
#   - a alias
#   - p port
#   - f function
#   Key/value pairs
#   - roles:def    This tag is defined here
#   - roles:imported    This tag is imported here
#   - type:Thing    This constructor is of type Thing.
#
# To do:
# - Tag an anonymous function with an anonymous name
# - Symbolic binary operators: +, - >>, etc.
# - Symbolic functions as prefix notation: Eg (+) 1 2
# - Signatures for functions, types and constructors



%prefix "pelmish"

%auxil	"struct parserCtx *"

%earlysource {
    #include "general.h"
}

%header {
	struct parserCtx;
}

%source {
#include "elmish_pre.h"
#include "routines.h"
}

# Top level elements

file <-
    <( parsable
    / unparsable
    )*> EOF

parsable <-
    ( <_>
    / functionDefinition
    )

unparsable <- <Non_WS+>

# Main Elm grammar

functionDefinition <-
    <identifier> _* <parameterList?> _* '=' _* expression {
        int r = makeElmTag(auxil, $1, $1s, K_FUNCTION, ELM_ROLE_DEFINED, false);
        addElmSignature(r, $2);
    }

anonymousFunction <-
    '\\' _* parameterList? _* '->' _* expression

# Mid level tokens -------------------------------------------------------

# For the signature field, we should exclude leading and trailing whitespace
#
parameterList <-
    ( literal _* parameterList )+
    / literal

expression <-
    '(' _* expression _* ')'
    / anonymousFunction
    / literal _* '+' _* literal
    / literal

# Low level tokens

identifier <- [A-Za-z_] [A-Za-z0-9_]*

literal <- identifier / [0-9]+

# Ignorable things

delimitedComment <- '{-' (delimitedComment / !'-}' .)* '-}'

lineComment <- '--' Non_NL* (NL / EOF)

# One non-empty ignorable unit
_ <- WS+ / NL / lineComment / delimitedComment

WS <- [ \t]+
NL <- '\n' / '\f' / '\r' '\n'?
Non_NL <- [^\n\r\f]
Non_WS <- [^ \t\n\r\f]
EOF <- !.

%%
#include "elmish_post.h"
